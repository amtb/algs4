plugins {
  id 'java'
  id 'com.adarshr.test-logger' version '2.1.1'
}

group 'org.algs4.collinear'
version '0.1'

repositories {
  jcenter()
}

test {
  dependsOn cleanTest
  useJUnitPlatform()
  testLogging.showStandardStreams = true
}

sourceSets {
  main {
    java {
      srcDirs = ['src']
    }
  }
  test {
    java {
      srcDirs = ['test']
    }
  }
}

dependencies {
  implementation files('../libs/algs4.jar')
  testImplementation 'org.junit.jupiter:junit-jupiter-api:5.6.0'
  testImplementation 'org.junit.jupiter:junit-jupiter-params:5.6.0'
  testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
}

task submissionZip(type: Zip) {
    archiveFileName = "collinear.zip"
    destinationDirectory = file("$projectDir")

    from "$projectDir/src"
    include "Point.java", "BruteCollinearPoints.java", "FastCollinearPoints.java"
}

task run(type: JavaExec) {
  classpath = sourceSets.main.runtimeClasspath
  main = 'Main'

  args "$projectDir/test-files/input6.txt"

  debugOptions {
     enabled = false
     port = 5566
     server = true
     suspend = true
  }
}
